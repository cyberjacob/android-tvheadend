plugins {
    id 'com.gladed.androidgitversion' version '0.3.4'
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "ie.macinnes.tvheadend"
        setProperty("archivesBaseName", applicationId)

        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        multiDexEnabled true

        setProperty("archivesBaseName", applicationId)

        buildConfigField "boolean", "ACRA_ENABLED", "false"
        buildConfigField "String", "ACRA_REPORT_URI", "\"\""
    }

    signingConfigs {
       release {
            storeFile file("../android-keystore/keystore.jks")
            storePassword System.getenv('keystore_password')
            keyAlias "key0"
            keyPassword System.getenv('keystore_password')
        }

    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"

            buildConfigField "boolean", "ACRA_ENABLED", "false"
            buildConfigField "String", "ACRA_REPORT_URI", "\"\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            buildConfigField "boolean", "ACRA_ENABLED", "true"
            buildConfigField "String", "ACRA_REPORT_URI", "\"" + System.getenv('acra_uri') + "\""
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'ie.macinnes.htsp:android-htsp:v0.0.2'
    // Used for testing local HTSP lib builds
//    compile(name: 'library-debug', ext: 'aar')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:leanback-v17:25.3.1'
    compile 'com.android.support:preference-leanback-v17:25.3.1'
    compile 'com.google.android.exoplayer:exoplayer-core:r2.4.3k1'
    compile 'com.google.android.exoplayer:exoplayer-ui:r2.4.3k1'
    compile 'com.google.android.exoplayer:extension-ffmpeg:r2.4.3k1'
    // Used for testing local exoplayer builds
//    compile(name: 'library-core-release', ext: 'aar')
//    compile(name: 'library-ui-release', ext: 'aar')
//    compile(name: 'extension-ffmpeg-release', ext: 'aar')
    compile 'ch.acra:acra:4.9.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
